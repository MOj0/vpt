// #part /glsl/shaders/renderers/MCM/integrate/vertex

#version 300 es

uniform int uLen;

out vec2 vPosition;
out vec2 vPositionRay;

vec4 getNodeImportance(ivec2 pos, int mipLevel){
    return vec4(0.25);
    // float i1 = texelFetch(uFrame, pos, mipLevel).r;
    // float i2 = texelFetch(uFrame, pos + ivec2(1, 0), mipLevel).r;
    // float i3 = texelFetch(uFrame, pos + ivec2(0, 1), mipLevel).r;
    // float i4 = texelFetch(uFrame, pos + ivec2(1, 1), mipLevel).r;
    // float sum = i1 + i2 + i3 + i4;

    // if (abs(sum) < 0.001) {
    //     return vec4(0.25);
    // }

    // return vec4(i1 / sum, i2 / sum, i3 / sum, i4 / sum);
}

int getRegion(vec4 regionImportance, float random) {
    float cumulativeProbability = 0.0;

    for (int i = 0; i < 4; i++) {
        cumulativeProbability += regionImportance[i];
        if (random <= cumulativeProbability) {
            return i;
        }
    }

    return 0; // Unreachable...
}

void main() {
    ivec2 texSize = ivec2(uLen);
    float y = float(gl_VertexID / texSize.x) / float(texSize.y);
    float x = float(gl_VertexID % texSize.x) / float(texSize.x);
    vec2 position = vec2(x, y) * 2.0 - 1.0;

    /// Random sampling
    // ivec2 currPos = ivec2(0);
    // for (int mipLevel = 6; mipLevel > 0; mipLevel--) {
    //     float random = fract(cos(float(gl_VertexID) + float(mipLevel) * 0.123) * 43758.5453123);
    //     vec4 regionImportance = getNodeImportance(currPos, mipLevel);
    //     int region = getRegion(regionImportance, random);

    //     if (region == 0) {
    //         // Top left quadrant
    //         currPos = 2 * currPos;
    //     }
    //     else if (region == 1) {
    //         // Top right quadrant
    //         currPos = 2 * (currPos + ivec2(1, 0));
    //     }
    //     else if (region == 2) {
    //         // Bottom left quadrant
    //         currPos = 2 * (currPos + ivec2(0, 1));
    //     } else {
    //         // Bottom right quadrant
    //         currPos = 2 * (currPos + ivec2(1, 1));
    //     }
    // }

    // ivec2 texSize = textureSize(uFrame, 0);
    // ivec2 texSize = ivec2(512);
    // ivec2 texSizeHalf = texSize / 2;
    // vec2 positionRay = vec2(currPos - texSizeHalf) / vec2(texSizeHalf);
    vec2 positionRay = vec2(fract(cos(float(gl_VertexID) + 0.123) * 43758.5453123)) * 2.0 - 1.0;
    ///

    vPosition = position;
    vPositionRay = positionRay;
    gl_Position = vec4(position, 0, 1);
    // gl_PointSize = 3.0;
}

// #part /glsl/shaders/renderers/MCM/integrate/fragment

#version 300 es
precision mediump float;
precision mediump sampler2D;
precision mediump sampler3D;

#define EPS 1e-5

// #link /glsl/mixins/Photon
@Photon
// #link /glsl/mixins/intersectCube
@intersectCube

@constants
@random/hash/pcg
@random/hash/squashlinear
@random/distribution/uniformdivision
@random/distribution/square
@random/distribution/disk
@random/distribution/sphere
@random/distribution/exponential

@unprojectRand

uniform sampler2D uPosition;
uniform sampler2D uDirection;
uniform sampler2D uTransmittance;
uniform sampler2D uRadiance;

uniform sampler3D uVolume;
uniform sampler2D uTransferFunction;
uniform sampler2D uEnvironment;

uniform mat4 uMvpInverseMatrix;
uniform vec2 uInverseResolution;
uniform float uRandSeed;
uniform float uBlur;

uniform float uExtinction;
uniform float uAnisotropy;
uniform uint uMaxBounces;
uniform uint uSteps;

in vec2 vPosition; // Grid position
in vec2 vPositionRay; // Ray position

layout (location = 0) out vec4 oPosition;
layout (location = 1) out vec4 oDirection;
layout (location = 2) out vec4 oTransmittance;
layout (location = 3) out vec4 oRadiance;
layout (location = 4) out vec4 oPositionRay;

void resetPhoton(inout uint state, inout Photon photon) {
    vec3 from, to;
    unprojectRand(state, vPosition, uMvpInverseMatrix, uInverseResolution, uBlur, from, to);
    photon.direction = normalize(to - from);
    photon.bounces = 0u;
    vec2 tbounds = max(intersectCube(from, photon.direction), 0.0);
    photon.position = from + tbounds.x * photon.direction;
    photon.transmittance = vec3(1);
}

vec4 sampleEnvironmentMap(vec3 d) {
    vec2 texCoord = vec2(atan(d.x, -d.z), asin(-d.y) * 2.0) * INVPI * 0.5 + 0.5;
    return texture(uEnvironment, texCoord);
}

vec4 sampleVolumeColor(vec3 position) {
    vec2 volumeSample = texture(uVolume, position).rg;
    vec4 transferSample = texture(uTransferFunction, volumeSample);
    return transferSample;
}

float sampleHenyeyGreensteinAngleCosine(inout uint state, float g) {
    float g2 = g * g;
    float c = (1.0 - g2) / (1.0 - g + 2.0 * g * random_uniform(state));
    return (1.0 + g2 - c * c) / (2.0 * g);
}

vec3 sampleHenyeyGreenstein(inout uint state, float g, vec3 direction) {
    // generate random direction and adjust it so that the angle is HG-sampled
    vec3 u = random_sphere(state);
    if (abs(g) < EPS) {
        return u;
    }
    float hgcos = sampleHenyeyGreensteinAngleCosine(state, g);
    vec3 circle = normalize(u - dot(u, direction) * direction);
    return sqrt(1.0 - hgcos * hgcos) * circle + hgcos * direction;
}

float max3(vec3 v) {
    return max(max(v.x, v.y), v.z);
}

float mean3(vec3 v) {
    return dot(v, vec3(1.0 / 3.0));
}

void main() {
    Photon photon;
    // Sample with the grid position
    vec2 mappedPosition = vPosition * 0.5 + 0.5;
    photon.position = texture(uPosition, mappedPosition).xyz;
    vec4 directionAndBounces = texture(uDirection, mappedPosition);
    photon.direction = directionAndBounces.xyz;
    photon.bounces = uint(directionAndBounces.w + 0.5);
    photon.transmittance = texture(uTransmittance, mappedPosition).rgb;
    vec4 radianceAndSamples = texture(uRadiance, mappedPosition);
    photon.radiance = radianceAndSamples.rgb;
    photon.samples = uint(radianceAndSamples.w + 0.5);

    uint state = hash(uvec3(floatBitsToUint(mappedPosition.x), floatBitsToUint(mappedPosition.y), floatBitsToUint(uRandSeed)));
    for (uint i = 0u; i < uSteps; i++) {
        float dist = random_exponential(state, uExtinction);
        photon.position += dist * photon.direction;

        vec4 volumeSample = sampleVolumeColor(photon.position);

        float PNull = 1.0 - volumeSample.a;
        float PScattering;
        if (photon.bounces >= uMaxBounces) {
            PScattering = 0.0;
        } else {
            PScattering = volumeSample.a * max3(volumeSample.rgb);
        }
        float PAbsorption = 1.0 - PNull - PScattering;

        float fortuneWheel = random_uniform(state);
        if (any(greaterThan(photon.position, vec3(1))) || any(lessThan(photon.position, vec3(0)))) {
            // out of bounds
            vec4 envSample = sampleEnvironmentMap(photon.direction);
            vec3 radiance = photon.transmittance * envSample.rgb;
            photon.samples++;
            photon.radiance += (radiance - photon.radiance) / float(photon.samples);
            resetPhoton(state, photon);
        } else if (fortuneWheel < PAbsorption) {
            // absorption
            vec3 radiance = vec3(0);
            photon.samples++;
            photon.radiance += (radiance - photon.radiance) / float(photon.samples);
            resetPhoton(state, photon);
        } else if (fortuneWheel < PAbsorption + PScattering) {
            // scattering
            photon.transmittance *= volumeSample.rgb;
            photon.direction = sampleHenyeyGreenstein(state, uAnisotropy, photon.direction);
            photon.bounces++;
        } else {
            // null collision
        }
    }

    oPosition = vec4(photon.position, 0);
    oDirection = vec4(photon.direction, float(photon.bounces));
    oTransmittance = vec4(photon.transmittance, 0);
    oRadiance = vec4(photon.radiance, float(photon.samples));
    oPositionRay = vec4(1, 2, 3, 4);
}

// #part /glsl/shaders/renderers/MCM/render/vertex

#version 300 es
precision mediump sampler2D;

uniform sampler2D uPositionRay;

out vec2 vPosition;

void main() {
    ivec2 texSize = ivec2(128);
    float y = float(gl_VertexID / texSize.x) / float(texSize.y);
    float x = float(gl_VertexID % texSize.x) / float(texSize.x);
    vec2 positionGridNormalized = vec2(x, y);

    vec2 positionRayClip = positionGridNormalized * 2.0 - 1.0;
    // vec2 positionRayClip = texture(uPositionRay, positionGridNormalized).xy;

    vPosition = positionGridNormalized;
    gl_Position = vec4(positionRayClip, 0, 1);
    gl_PointSize = 8.0;
}

// #part /glsl/shaders/renderers/MCM/render/fragment

#version 300 es
precision mediump float;
precision mediump sampler2D;

uniform sampler2D uColor;

in vec2 vPosition;

out vec4 oColor;

void main() {
    oColor = vec4(texture(uColor, vPosition).rgb, 1);
}

// #part /glsl/shaders/renderers/MCM/reset/vertex

#version 300 es

const vec2 vertices[] = vec2[](
    vec2(-1, -1),
    vec2( 3, -1),
    vec2(-1,  3)
);

out vec2 vPosition;

void main() {
    vec2 position = vertices[gl_VertexID];
    vPosition = position;
    gl_Position = vec4(position, 0, 1);
}

// #part /glsl/shaders/renderers/MCM/reset/fragment

#version 300 es
precision mediump float;

// #link /glsl/mixins/Photon
@Photon
// #link /glsl/mixins/intersectCube
@intersectCube

@constants
@random/hash/pcg
@random/hash/squashlinear
@random/distribution/uniformdivision
@random/distribution/square
@random/distribution/disk
@random/distribution/sphere
@random/distribution/exponential

@unprojectRand

uniform mat4 uMvpInverseMatrix;
uniform vec2 uInverseResolution;
uniform float uRandSeed;
uniform float uBlur;

in vec2 vPosition;

layout (location = 0) out vec4 oPosition;
layout (location = 1) out vec4 oDirection;
layout (location = 2) out vec4 oTransmittance;
layout (location = 3) out vec4 oRadiance;
layout (location = 4) out vec2 oPositionRay;

void main() {
    Photon photon;
    vec3 from, to;
    uint state = hash(uvec3(floatBitsToUint(vPosition.x), floatBitsToUint(vPosition.y), floatBitsToUint(uRandSeed)));
    unprojectRand(state, vPosition, uMvpInverseMatrix, uInverseResolution, uBlur, from, to);
    photon.direction = normalize(to - from);
    vec2 tbounds = max(intersectCube(from, photon.direction), 0.0);
    photon.position = from + tbounds.x * photon.direction;
    photon.transmittance = vec3(1);
    photon.radiance = vec3(1);
    photon.bounces = 0u;
    photon.samples = 0u;
    oPosition = vec4(photon.position, 0);
    oDirection = vec4(photon.direction, float(photon.bounces));
    oTransmittance = vec4(photon.transmittance, 0);
    oRadiance = vec4(photon.radiance, float(photon.samples));
    oPositionRay = vec2(0);
}
